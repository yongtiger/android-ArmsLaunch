

if (isBuildModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

///[ArmsComponent#butterknife]
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]
    useLibrary 'org.apache.http.legacy'

    ///[ArmsComponent#enable Java 8 language features]
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner rootProject.ext.android["testInstrumentationRunner"]
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
                includeCompileClasspath true
            }
        }
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            buildConfigField "boolean", "IS_BUILD_MODULE", "${isBuildModule}"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            buildConfigField "boolean", "IS_BUILD_MODULE", "${isBuildModule}"
            minifyEnabled true
            if (isBuildModule.toBoolean()) {
                shrinkResources true
            }
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable "ResourceType"
        abortOnError false
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isBuildModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    if (isBuildModule.toBoolean()) {
        ///[ArmsComponent#butterknife]
        annotationProcessor(rootProject.ext.dependencies["butterknife-compiler"]) {
            exclude module: 'support-annotations'
        }

        ///[ArmsComponent#dagger2]
        annotationProcessor rootProject.ext.dependencies["dagger2-compiler"]

        ///[ArmsComponent#arouter]
        annotationProcessor rootProject.ext.dependencies["arouter-compiler"]

        ///[ArmsComponent#leakcanary]
        debugImplementation rootProject.ext.dependencies["leakcanary-debug"]
        releaseImplementation rootProject.ext.dependencies["leakcanary-release"]
        testImplementation rootProject.ext.dependencies["leakcanary-release"]

    } else {
        ///[ArmsComponent#butterknife]
        compileOnly rootProject.ext.dependencies["butterknife-compiler"]

        ///[ArmsComponent#dagger2]
        compileOnly rootProject.ext.dependencies["dagger2-compiler"]

        ///[ArmsComponent#arouter]
        compileOnly rootProject.ext.dependencies["arouter-compiler"]

        ///[ArmsComponent#leakcanary]
        compileOnly rootProject.ext.dependencies["leakcanary-debug"]
        compileOnly rootProject.ext.dependencies["leakcanary-release"]
    }

    ///[FIX BUG#androidTest and Test]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]

}

